plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.xayah.databackup'
    compileSdk 33

    defaultConfig {
        applicationId "com.xayah.databackup"
        minSdk 28
        targetSdk 33
        versionCode 98
        versionName "1.0.1-alpha02"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions "common", "feature"
    productFlavors {
        "arm64-v8a" {
            dimension "common"
        }
        "armeabi-v7a" {
            dimension "common"
        }
        "x86" {
            dimension "common"
        }
        "x86_64" {
            dimension "common"
        }
        "foss" {
            dimension "feature"
        }
        "premium" {
            dimension "feature"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("Boolean", "ENABLE_VERBOSE", 'false')
        }
        debug {
            buildConfigField("Boolean", "ENABLE_VERBOSE", 'true')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.0"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "DataBackup-${variant.versionName}-${variant.productFlavors[0].name}-${variant.productFlavors[1].name}-${variant.buildType.name}.apk".replaceAll("\\*", "")
            }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // LibSU
    implementation "com.github.topjohnwu.libsu:core:$libsu_version"
    implementation project(path: ':librootservice')
    implementation project(path: ':libhiddenapi')

    // MultiType
    implementation 'com.drakeet.multitype:multitype:4.3.0'

    // Lottie
    implementation 'com.airbnb.android:lottie:5.2.0'

    // MaterialYouFileExplorer
    implementation 'com.github.XayahSuSuSu:Android-MaterialYouFileExplorer:1.3.2'

    // zip4j
    implementation 'net.lingala.zip4j:zip4j:2.10.0'

    // okhttp3
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    // gson
    implementation 'com.google.code.gson:gson:2.9.1'

    // splashscreen
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // Markwon
    implementation "io.noties.markwon:core:4.6.2"

    // PermissionX
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'

    /**
     * Compose
     */
    def composeBom = platform('androidx.compose:compose-bom:2023.03.00')
    implementation composeBom
    androidTestImplementation composeBom
    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.0'
    implementation "androidx.compose.ui:ui"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
    // Material Design 3
    implementation 'androidx.compose.material3:material3:1.1.0-beta02'
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Accompanist
    def accompanistVersion = '0.29.2-rc'
    implementation "com.google.accompanist:accompanist-insets:$accompanistVersion"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanistVersion"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanistVersion"
    implementation "com.google.accompanist:accompanist-permissions:$accompanistVersion"
    implementation "com.google.accompanist:accompanist-drawablepainter:$accompanistVersion"

    // Firebase
    "premiumImplementation" platform('com.google.firebase:firebase-bom:31.2.2')
    "premiumImplementation" 'com.google.firebase:firebase-analytics-ktx'
    "premiumImplementation" 'com.google.firebase:firebase-crashlytics-ktx'
}
